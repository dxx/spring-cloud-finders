plugins {
    id "java-platform"
}

description = "Spring Cloud Finders Bom"

group "io.github.dxx"
version "0.1.0"

javaPlatform {
    allowDependencies()
}

dependencies {
    api platform("org.springframework.boot:spring-boot-dependencies:${libraries.springBootVersion}")
    api platform("org.springframework.cloud:spring-cloud-dependencies:${libraries.springCloudVersion}")

    constraints {
        api "io.github.dxx:spring-cloud-finders-discovery:${project.version}"
        api "io.github.dxx:finders-client:${libraries.findersClientVersion}"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.javaPlatform

            pom {
                afterEvaluate {
                    name = project.name
                    description = project.description
                }

                url = "https://github.com/dxx/spring-cloud-finders"

                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        id = "dxx"
                        name = "dxx"
                        email = "dx1898@foxmail.com"
                    }
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}
